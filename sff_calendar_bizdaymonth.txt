[LegacyColorValue = true]; 

{
Function    : sff_calendar_bizdaymonth
Description : Get the ordinal business day of the month
Reference	: Calendrical Calculations, Cambridge Press, 2002

Copyright 2024 Scottfree Gains LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
}

Inputs:
	RDate(Numeric);

Variables:
	GYear(0),
	GMonth(0),
	GDay(0),
	RDate1(0),
	ib(0),
	id(0),
	Holidays(0),
	ih(0);

Arrays:
	Holiday[20](0),
	HolidayName[20]("");	

Condition1 = sff_calendar_rdate2gdate(RDate, GYear, GMonth, GDay);
RDate1 = sff_calendar_gdate2rdate(GYear, GMonth, 1);
Holidays = sff_calendar_setholidays(GYear, True, Holiday, HolidayName);
ib = 0;
id = RDate1;
While id <= RDate Begin
	Value1 = sff_calendar_dayofweek(id);
	Condition1 = Value1 >= 1 and Value1 <= 5;
	If Condition1 Then
		ib = ib + 1;
	For ih = 0 To Holidays-1 Begin
		Value2 = sff_calendar_dayofweek(Holiday[ih]);
		Condition2 = Value2 >= 1 and Value2 <= 5;
		If Holiday[ih] = id and Condition2 Then
			ib = ib - 1;
	End;
	id = id + 1;
End;
sff_calendar_bizdaymonth = ib;
